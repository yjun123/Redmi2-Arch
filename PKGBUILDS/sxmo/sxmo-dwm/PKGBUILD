# Maintainer: dni <office@dnilabs.com>
pkgname=sxmo-dwm
pkgver=6.2.17
pkgrel=1
pkgdesc="Dwm fork for Sxmo UI; supports volume-key hotkeys, swallow, keyboard, among other patches"
arch=('x86_64' 'armv7h' 'aarch64')
url="https://git.sr.ht/~mil/sxmo-dwm"
license=('MIT')
depends=('xorg-server' 'freetype2' 'libx11' 'libxinerama' 'libxft')
provides=('dwm')
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~mil/sxmo-dwm/archive/$pkgver.tar.gz")

prepare() {
  sed -i -e '/CFLAGS/{s/-Os//;s/=/+=/}' \
    -e '/LDFLAGS/{s/=/+=/}' "$pkgname-$pkgver/config.mk"
}

build() {
  cd "$pkgname-$pkgver"
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}

sha512sums=('2e07f97beb1104f4c792df8a4895a7c89c0159659343833c2dac1a8c7b8089e348f9d676f23b05b04857b9134c345ef2de100a826a968773761064ba4dc214db')
