# Maintainer: Yjun <jerrysteve1101 at gmail dot com>
pkgname=device-xiaomi-redmi2
pkgver=0.1
pkgrel=1
pkgdesc="Redmi 2 device package"
arch=(aarch64)
url="https://github.com/yjun123/Redmi2-Arch"
license=('MIT')
depends=(danctnix-tweaks linux-msm8916 firmware-redmi2 alsa-ucm-redmi2)
# install="$pkgname.install"
source=(10-proximity.rules
        redmi2-suspend-hook.sh
        machine-info
        pulseaudio-redmitweaks.conf)

package() {
    # Kanged from Mobian, this is to make iio-sensor-proxy works with
    # proximity sensor.
    install -Dm644 "$srcdir"/10-proximity.rules \
        "$pkgdir"/usr/lib/udev/rules.d/10-proximity.rules

    # Some workaround for suspend
    install -Dm755 "$srcdir"/redmi2-suspend-hook.sh \
        "$pkgdir"/usr/lib/systemd/system-sleep/redmi2-suspend-hook.sh

    install -Dm644 "$srcdir"/machine-info "$pkgdir"/etc/machine-info

    install -Dm644 "$srcdir"/pulseaudio-redmitweaks.conf \
        "$pkgdir"/etc/pulse/daemon.conf.d/redmitweaks.conf
}

md5sums=('b72ff096c022588f72c226283ac328bd'
         'b0912b95d7d810a0f161d9bd79fc162b'
         'ce19ee6617e0d9d27066cd3c3c35f45f'
         '1504e684e5a2aec439e0674cdfc9d3c1')
