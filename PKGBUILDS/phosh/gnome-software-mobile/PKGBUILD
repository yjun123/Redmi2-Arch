# Maintainer: Danct12 <danct12@disroot.org>
# Contribotor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software-mobile
_pkgname=gnome-software
pkgver=42.2
pkgrel=1
pkgdesc="GNOME Software Tools (w/ Purism patches)"
url="https://gitlab.gnome.org/GNOME/gnome-software"
arch=(x86_64 armv7h aarch64)
license=(GPL2)
makedepends=(appstream gsettings-desktop-schemas libpackagekit-glib flatpak
             fwupd docbook-xsl git gobject-introspection gtk-doc meson
             valgrind gnome-online-accounts libxmlb malcontent gtk4 libadwaita
             libsysprof-capture)
_commit=09cafc9ef92587b3d3326d3fa751328c0a28dcf9  # tags/42.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-software.git#commit=$_commit"
        '0001-Revert-Launch-software-properties-gtk-in-place-of-the-sources-dialog.patch')
sha256sums=('SKIP'
            'd454459d5b749f06da05a5a05cc7dad4097cc2c68297e91ead3126f05b007036')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  # Ensure static library is non-LTO compatible
  CFLAGS+=" -ffat-lto-objects"

  # Building with soup3 requires flatpak to be built with soup3
  arch-meson $_pkgname build -D soup2=true
  meson compile -C build
}

check() {
  # build container troubles
  meson test -C build --print-errorlogs || :
}

package() {
  provides=(gnome-software gnome-software-packagekit-plugin)
  conflicts=(gnome-software gnome-software-packagekit-plugin)
  depends=(libxmlb gsettings-desktop-schemas libpackagekit-glib gtk4 libadwaita
           gnome-online-accounts appstream libsysprof-capture
           archlinux-appstream-data packagekit)
  optdepends=('flatpak: Flatpak support plugin'
              'fwupd: fwupd support plugin'
              'malcontent: Parental control plugin')

  meson install -C build --destdir "$pkgdir"
}
