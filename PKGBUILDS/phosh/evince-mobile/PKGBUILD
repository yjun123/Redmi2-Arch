# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=evince
pkgname=$_pkgname-mobile
pkgver=42.3
pkgrel=1
pkgdesc="Document viewer (PDF, PostScript, XPS, djvu, dvi, tiff, cbr, cbz, cb7, cbt)"
url="https://wiki.gnome.org/Apps/Evince"
arch=('x86_64' 'armv7h' 'aarch64')
license=('GPL')
_commit=0ff17893679b05e7635e76e5c77384995b802f81  # tags/42.3^0
source=("git+https://gitlab.gnome.org/GNOME/evince.git#commit=$_commit"
        '0001-toolbar-Allow-to-toggle-the-sidebar-button.patch'
        '0002-toolbar-Allow-hiding-zoom-annotation-and-page-widgets.patch'
        '0003-properties-view-Reduce-the-label-width-chars.patch'
        '0004-window-Port-it-to-phones.patch')
sha256sums=('SKIP'
            '39cf66991132953400d6825d4aa671dabe37538ac543ddd5ccce73d6217927c2'
            '70274e065c512593d6f82865e48090c2ec9440fbfbfcfb505fa0e99731de3f39'
            '9573069449b6eeca2157fb55976bed217076427c23863870159ce83ef4727539'
            'b81b9747537fb478e5bdb056062211d2ed2c5dd43b2afa75fc3ddae2667da6a0')
provides=(libev{document,view}3.so $_pkgname $_pkgname-lib-docs)
conflicts=($_pkgname)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre dconf libsecret libsynctex
         gsettings-desktop-schemas gnome-desktop libarchive gst-plugins-base-libs gspell libhandy
         gvfs)
makedepends=(libnautilus-extension texlive-bin gobject-introspection gi-docgen git meson
             appstream-glib yelp-tools)
optdepends=(
  'texlive-bin: DVI support'
)

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build -D ps=enabled
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}
