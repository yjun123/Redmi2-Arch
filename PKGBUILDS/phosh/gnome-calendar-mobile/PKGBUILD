# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-calendar-mobile
_pkgname=gnome-calendar
pkgver=45.0
pkgrel=1
pkgdesc="Simple and beautiful calendar application designed to perfectly fit the GNOME desktop"
url="https://wiki.gnome.org/Apps/Calendar"
arch=(x86_64 armv7h aarch64)
license=(GPL)
depends=(
  dconf
  evolution-data-server
  geoclue
  geocode-glib-2
  glib2
  graphene
  gsettings-desktop-schemas
  gtk4
  hicolor-icon-theme
  libadwaita
  libedataserverui4
  libgweather-4
  libical
  libsoup3
  pango
)
makedepends=(
  git
  meson
)
optdepends=('gnome-control-center: Manage online accounts')
provides=('gnome-calendar')
conflicts=('gnome-calendar')
_commit=619d95c3367691b16b80b69e70dcaa48f3bf1033  # tags/45.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-calendar.git#commit=$_commit"
        '0001-gcal-event-popover-draw-on-top-of-event-widget.patch')
sha256sums=('SKIP'
            '976490a7b9912df87d8e8607cb787100b628583f0a099596e0757e44b2d4ff7e')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgname

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  arch-meson $_pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim:set ts=2 sw=2 et:
