# Maintainer: Danct12 <danct12@disroot.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgname=gnome-control-center
pkgname=gnome-control-center-mobile
pkgver=42.2
pkgrel=1
pkgdesc="GNOME's main interface to configure various aspects of the desktop - Purism fork"
url="https://gitlab.gnome.org/GNOME/gnome-control-center"
license=(GPL2)
arch=(x86_64 armv7h aarch64)
depends=(accountsservice cups-pk-helper gnome-bluetooth-3.0 gnome-desktop-4
         gnome-online-accounts gnome-settings-daemon gsettings-desktop-schemas
         gtk4 libgtop libnma-gtk4 sound-theme-freedesktop upower libpwquality
         gnome-color-manager smbclient libmm-glib libgnomekbd libibus libgudev
         bolt udisks2 libadwaita gsound colord-gtk4 gcr modemmanager)
makedepends=(docbook-xsl git python meson)
checkdepends=(python-dbusmock python-gobject xorg-server-xvfb)
optdepends=('system-config-printer: Printer settings'
            'gnome-user-share: WebDAV file sharing'
            'gnome-remote-desktop: screen sharing'
            'rygel: media sharing'
            'openssh: remote login'
            'power-profiles-daemon: Power profiles support')
provides=(gnome-control-center)
conflicts=(gnome-control-center)
_commit=0bbcc3b8b30583908be7ec129b63bb40d8697b7b  # tags/42.2^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-control-center.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        # purism
        0001-avatar-chooser-Adapt-to-work-on-librem5.patch
        0002-datetime-Fix-timezone-selection-map.patch
        0003-display-Request-a-smaller-size-for-arrangment-widget.patch
        0005-power-show-brightness-slider-in-a-separate-row.patch
        0006-sound-fix-test-dialog-on-small-screen.patch
        Add-patches-to-check-if-phone.patch
        network-connection-editor-Allow-dialog-to-resize.patch
        0002-UserAccount-fix-window-resizing-issue.patch
        # mobian
        0004-power-add-more-suspend-timing-options.patch
        resize-connection-editor.patch
        # upstream
        0001-common-Wrap-CcPermissionInfobar-labels-to-fit-in-nar.patch
        0001-display-Wrap-Night-Light-GtkInfoBar-label-to-fit-in-.patch
        0001-printers-Wrap-job-dialog-GtkInfoBar-label-to-fit-in-.patch
        0001-removable-media-Wrap-GtkCheckButton-label-to-fit-nar.patch)
sha256sums=('SKIP'
            'SKIP'
            '1170038f625f00ac2443cd593be0da9adbb0a2a0977dc775ba81a19b31d6444d'
            'c4571ada3ed0d37aca5072b87605b854f501c7f50b81530018ad6394ce9285f1'
            '75cef56c6d81b702c9d0bfcc6c47c041e19d2dbf47f5d25129771e41eaedfe53'
            'c7774bab64278759f0397996b04788bb5e3ad775c830c2efbd68b2a29f7cad04'
            '3d22e83c95f59cab92320d412918639f4c4fc8830832c8becbfa381dbb34c9aa'
            '30d9a86b2c3f0a3c41de57c0aed311023a70b3bc89be7547133ac646c20abf73'
            '20a7b6df2932ef3ab4bfb2073d306fb04693fb81fc91f8ec98e4d565d6d78011'
            '8708c8ba1df9b640f61d49ce28a3ea9610eba1ab290be5bf6b1136176db4a7ec'
            '8fa599ef61a759dcebbb86db1b4c079532db1749c913b32a170bdeea28c43865'
            '5c5110daef890b354845497522450f396c246053fab5bd8c1d80708f400b8fcb'
            '36fbef62590ecaeffa56de996703acf6a58c4d47d2c97518db238852555ef393'
            'c6096dc4d6a694ec66785bf96202bce88309d7e1cae7ef087c1ee55d4a1e7156'
            '3a706ee02dbf5ec51cde8260b0d4dd6e51d0e0f3f34ff2434999e3a6ec19d9dc'
            '9b0e092f5af3190c348eb5d72594f9345b7320d19e38b9c6e5f4c4f10ae67e27')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd ${_pkgname}

  # Install bare logos into pixmaps, not icons
  sed -i "/install_dir/s/'icons'/'pixmaps'/" panels/info-overview/meson.build

  git submodule init subprojects/gvc
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule update

  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  local meson_options=(
    -D documentation=true
  )

  arch-meson ${_pkgname} build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -d -o root -g 102 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  echo "X-Purism-FormFactor=Workstation;Mobile;" >> "$pkgdir/usr/share/applications/gnome-control-center.desktop"
}
